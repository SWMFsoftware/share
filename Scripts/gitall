#!/usr/bin/perl -s

if($h or $help){
    print "
Recursively find all subdirectories that are Git repositories
and execute the Git command passed in the arguments.
The verbose output from Git status is suppressed unless the -v switch is used.

Usage:
  gitall [-h] [-v] [-l] [-q] [-s[=N]] [-r[=c|=f]] [-remote[=URL]] [COMMANDS]

-h -help      print this help message
-l -list      list all subdirectories that are repositories
-v -verbose   provide verbose information for the status command
-q -quiet     execute the Git command without printing the gitall annotations
-s -sleep     sleep 5 seconds after each Git command to avoid ssh access issues
-s=N -sleep=N sleep N seconds after each Git push, pull or fetch command
     	      The default is set by the \$GITHUBSLEEP environment variable.
-r -reference print Git references 
-r=c          print Git references as a C header with a print statement
-r=f          print Git references as a Fortran write statement
-remote       switch remote to git@github.com:SWMFsoftware/
-remote=URL   switch remote to URL
COMMANDS      any Git command (cannot be combined with -r or -remote)

Examples:

Show verbose status of all Git subdirectories:
    gitall -v status

Pull remote version into all Git subdirectories and sleep 5 seconds after each:
    gitall -s pull

Print the references for each Git repository:
    gitall -r

Create a C header file to be compiled into the executable
    gitall -r=c > src/gitinfo.h

Switch to the UM GitHub remote server
    gitall -remote

";
    exit 0;
}

my $Verbose   = ($v or $verbose);
my $Sleep     = ($s or $sleep or $ENV{GITHUBSLEEP});
my $List      = ($l or $list);
my $Quiet     = ($q or $quiet);
my $Reference = ($r or $reference);
my $Remote    = ($remote);

use strict;

# Default remote
$Remote = 'git@github.com:SWMFsoftware' if $Remote eq "1";

my $gitcmd;                # The variable to receive the git command

$gitcmd = join(' ',@ARGV) . ' 2>&1'; # redirect STDERR to STDOUT
$gitcmd .= "; sleep $Sleep" if $Sleep and $gitcmd =~ /\b(push|pull|fetch)\b/;

my @RefList; 
my $pwd = `pwd`; chop($pwd);
my $remote = `git config remote.origin.url`; chomp($remote);
my $mainrepo = $1 if $remote =~ s/\/(\w+)$//;

if($gitcmd or $Reference or $List or $Remote){
    &execute_gitall($pwd);
}

my $Error;

@RefList = sort {$b !~ /^  / or ($a cmp $b)} @RefList;
if($Reference =~ /c/i){
    print '#include <iostream>
void print_git_info(){
  std::cout 
    << "Main dir=', $pwd,'\n"
    << "Remote  =', $remote,'\n"
    << "', join('\n"'.'
    << "', @RefList),'\n";
}
';
}elsif($Reference =~ /f/i){
    print "
  write(*,'(100(a/))') &
       'Main dir=",$pwd,"', &
       'Remote  =",$remote,"', &
       '", join("', &
       '", @RefList),"'
";
}else{
    print join("\n", @RefList),"\n";
}

warn "!!! number of gitall errors: $Error\n" if $Error;

exit 0;
#==============================================================================

sub execute_gitall(){

   my($dir) = @_;

   # Make sure pull is using rebase by default in this git repo.
   # Use git pull -rebase=false to overwrite this.
   `cd $dir; git config pull.rebase true` if -d "$dir/.git" 
       and not `grep 'rebase = true' $dir/.git/config`;
   
   opendir(my $DIR,"$dir") or die "gitall can't open directory $dir\n";
   my @gits= grep { /^.git$/ } readdir($DIR);
   closedir($DIR);
   opendir(my $DIR,"$dir") or die "gitall can't open directory $dir\n";
   # Exclude directories starting with . and symbolic links
   my @subdirs = 
       grep { /^[^.]/ and -d "$dir/$_" and not -l "$dir/$_"} sort(readdir($DIR));
   closedir($DIR);
   push @subdirs,@gits;

   my $subdir;
   foreach $subdir (@subdirs){
       if ($subdir eq ".git"){
	   if($List){
	       print "$dir\n";
	   }elsif ($Remote){
	       # get current remote repo name
	       my $reponame = `cd $dir; git config remote.origin.url`;
	       $reponame =~ s/.*\///;
	       # set new remote
	       `cd $dir; git remote set-url origin $Remote/$reponame`;
	   }elsif ($Reference){
	       my $reponame = "  $dir";
	       $reponame =~ s/$pwd\/// or $reponame = $mainrepo;
	       $reponame .= " " x (35-length $reponame);

	       # for each repo get the status
	       my $gitstatus = `cd $dir; git status`;

	       # figure out the branch
	       my $branch = "master";
	       $branch = $1 if $gitstatus =~ /^[\#\s]*On branch (\w+)/;
	       $branch = $1 if $gitstatus =~ /\bdetached at (\w+)/;

	       my $status = "original";
	       $status = "modified" if $gitstatus =~ /\bmodified:/;
	       
	       # get the git reference with date for the branch
	       my $localref = 
		   `cd $dir; git show $branch --no-patch --format="%cd %h" --date=short`;
	       $localref =~ s/\n.*//s;

	       # Add reference after the status
	       $status .= "  $branch $localref";

	       # Add leading spaces 
	       $status .= " " x (30-length $status);

	       # Add "detached" if not master or stable branch
	       $status .= " detached" if $branch !~ /^master|stable$/;

	       # Store status into RefList array
	       push @RefList, "$reponame $status";
	   }else{
	       my $result = `cd $dir; git $gitcmd`;
	       # remove verbose info
	       if(not $Verbose){
		   $result =~ s/[\#\s]*On branch master//;
		   $result =~ s/[\#\s]*Your branch is up.to.date with .origin\/master.+//;
		   $result =~ s/nothing to commit, working \w+ clean//;
		   $result =~ s/^\s*$//g;
	       }
	       if(not $Quiet){
		   print "=========== $dir:\n $result \n" if $result;
	       }else{
		   print "$result" if $result;
	       }
	       $Error++ if $result =~ /error/;
	   }
       }else{
           &execute_gitall("$dir/$subdir");
       }
   }
}






